
load "./types.maude" .

mod OPERATOR is
  sorts Operator Category .

  op category : Operator -> Category .
endm

mod EXPRESSION is
  pr TYPE .
  pr OPERATOR .

  sorts Expression .
  subsort Type < Expression .

  op type : Expression -> Type .
endm

mod LITERAL-EXPRESSION is
  pr EXPRESSION .

  sort Literal .
  subsort Literal < Expression .
endm

mod LITERAL-BOOLEAN is
  pr LITERAL-EXPRESSION .
  pr BOOLEAN-TYPE .

  op True : -> Literal .
  op False : -> Literal .

  eq type(True) = Boolean .
  eq type(False) = Boolean .
endm

mod LITERAL-INTEGER is
  pr LITERAL-EXPRESSION .
  pr NUMERIC-TYPES .
  pr QID .

  sort IntegerLiteral .
  subsort IntegerLiteral < Literal .

  op _i : Qid -> IntegerLiteral .
  var I : IntegerLiteral .
  eq type(I) = Integer .
endm

mod LITERAL-LONG is
  pr LITERAL-EXPRESSION .
  pr NUMERIC-TYPES .
  pr QID .

  sort LongLiteral .
  subsort LongLiteral < Literal .

  op _l : Qid -> LongLiteral .
  var L : LongLiteral .
  eq type(L) = Long .
endm

mod LITERAL-FLOAT is
  pr LITERAL-EXPRESSION .
  pr FLOATING-POINT-TYPES .
  pr QID .

  sort FloatLiteral .
  subsort FloatLiteral < Literal .

  op _f : Qid -> FloatLiteral .
  var F : FloatLiteral .
  eq type(F) = Float .
endm

mod LITERAL-STRING is
  pr LITERAL-EXPRESSION .
  pr STRING-TYPE .
  pr QID .

  sort StringLiteral .
  subsort StringLiteral < Literal .

  op _s : Qid -> StringLiteral .
  var S : StringLiteral .
  eq type(S) = String .
endm

mod LITERALS is
  pr LITERAL-BOOLEAN .
  pr LITERAL-INTEGER .
  pr LITERAL-LONG .
  pr LITERAL-FLOAT .
  pr LITERAL-STRING .
endm

mod INFIX-EXPRESSION is
  pr EXPRESSION .

  sort Infix .
  subsort Infix < Expression .

  op (_ _ _) : Expression Operator Expression -> Infix .
endm

mod LOGICAL-EXPRESSION is
  pr INFIX-EXPRESSION .

  op Logical : -> Category .
  ops and or : -> Operator .

  eq category(and) = Logical .
  eq category(or) = Logical .

  vars E1 E2 : Expression .
  var OP : Operator .

  pr BOOLEAN-TYPE .

  op logical : Expression Operator Expression -> Bool .
  eq logical(E1, OP, E2) = type(E1) == Boolean and type(E2) == Boolean and
                           category(OP) == Logical .

  ceq type(E1 OP E2) = Boolean if logical(E1, OP, E2) .

  op InvalidLogicalExpression : -> TypeError .

  ceq type(E1 OP E2) = Undefined(InvalidLogicalExpression)
    if not logical(E1, OP, E2) and category(OP) == Logical .
endm

mod ARITHMETIC-EXPRESSION is
  pr INFIX-EXPRESSION .

  op Arithmetic : -> Category .
  ops + - / * : -> Operator .

  eq category(+) = Arithmetic .
  eq category(-) = Arithmetic .
  eq category(/) = Arithmetic .
  eq category(*) = Arithmetic .

  vars E1 E2 : Expression .
  var OP : Operator .

  op arithmetic : Expression Operator Expression -> Bool .

  pr NUMERIC-TYPES .
  ceq arithmetic(E1, OP, E2) = category(OP) == Arithmetic
    if numeric(type(E1)) and numeric(type(E2)) .

  pr FLOATING-POINT-TYPES .
  ceq arithmetic(E1, OP, E2) = category(OP) == Arithmetic
    if floating_point(type(E1)) and floating_point(type(E2)) .

  ceq type(E1 OP E2) = type(E2)
    if arithmetic(E1, OP, E2) and type(E1) <: type(E2) .
  ceq type(E1 OP E2) = type(E1)
     if arithmetic(E1, OP, E2) and type(E2) <: type(E1) .

  op InvalidArithmeticExpression : -> TypeError .

  ceq type(E1 OP E2) = Undefined(InvalidArithmeticExpression)
    if category(OP) == Arithmetic .
endm

mod STRING-CONCATENATION is
  pr INFIX-EXPRESSION .
  pr STRING-TYPE .

  op Concatenation : -> Category .
  op & : -> Operator .

  eq category(&) = Concatenation .

  vars E1 E2 : Expression .
  var OP : Operator .

  op concat : Expression Operator Expression -> Bool .

  ceq concat(E1, OP, E2) = category(OP) == Concatenation
    if type(E1) == String and type(E2) == String .

  ceq type(E1 OP E2) = String if concat(E1, OP, E2) .

  op InvalidStringConcatenation : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidStringConcatenation)
    if category(OP) == Concatenation .
endm

mod REFERENTIAL-EXPRESSION is
  pr INFIX-EXPRESSION .
  pr REFERENTIAL-TYPE .
  pr BOOLEAN-TYPE .

  op ReferentialExpression : -> Category .
  ops === !== : -> Operator .
  eq category(===) = ReferentialExpression .
  eq category(!==) = ReferentialExpression .

  vars E1 E2 : Expression .
  var OP : Operator .

  op UnrelatedTypeComparison : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnrelatedTypeComparison)
    if (category(OP) == ReferentialExpression) /\
       not (elementaryTypeOf(type(E1)) <:> elementaryTypeOf(type(E2))) .

  ceq type(E1 OP E2) = Boolean
    if (category(OP) == ReferentialExpression) /\
       (type(E1) :: ReferentialType) /\
       (type(E2) :: ReferentialType) .

  op InvalidReferentialExpression : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidReferentialExpression)
    if category(OP) == ReferentialExpression .
endm

mod TYPE-CHECKING is
  pr INFIX-EXPRESSION .
  pr REFERENTIAL-TYPE .
  pr BOOLEAN-TYPE .

  op TypeChecking : -> Category .
  ops is isnt : -> Operator .
  eq category(is) = TypeChecking .
  eq category(isnt) = TypeChecking .

  vars E1 E2 : Expression .
  var OP : Operator .

  op UnrelatedTypeCheck : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnrelatedTypeCheck)
    if (category(OP) == TypeChecking) /\
       not (elementaryTypeOf(E2) <:> elementaryTypeOf(type(E1))) .

  --- op InvalidRequiredTypeCheck : -> TypeError .
  --- ceq type(E1 OP E2) = Undefined(InvalidRequiredTypeCheck)
  ---   if (category(OP) == TypeChecking) /\ not (type(E1) :: RequiredType) /\ (E2 :: RequiredType) .
  ---
  --- op InvalidOptionalTypeCheck : -> TypeError .
  --- ceq type(E1 OP E2) = Undefined(InvalidOptionalTypeCheck)
  ---   if (category(OP) == TypeChecking) /\ not (type(E1) :: OptionalType) /\ (E2 :: OptionalType) .
  ---
  --- op InvalidSequentialTypeCheck : -> TypeError .
  --- ceq type(E1 OP E2) = Undefined(InvalidSequentialTypeCheck)
  ---   if (category(OP) == TypeChecking) /\ not (type(E1) :: SequentialType) /\ (E2 :: SequentialType) .

  ceq type(E1 OP E2) = Boolean
    if (category(OP) == TypeChecking) /\
       (type(E1) :: ReferentialType) /\
       (E2 :: ReferentialType) .

  op InvalidTypeCheck : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidTypeCheck)
    if category(OP) == TypeChecking .
endm

mod TYPE-CASTING is
  pr INFIX-EXPRESSION .
  pr REFERENTIAL-TYPE .
  pr BOOLEAN-TYPE .

  op TypeCasting : -> Category .
  ops as as? as! : -> Operator .
  eq category(as) = TypeCasting .
  eq category(as?) = TypeCasting .
  eq category(as!) = TypeCasting .

  vars E1 E2 : Expression .
  var OP : Operator .

  op UnnecessarilyForcedTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnnecessarilyForcedTypeCast)
    if (OP == as!) /\ (type(E1) <: E2) .

  op UnnecessarilyConditionalTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnnecessarilyConditionalTypeCast)
    if (OP == as?) /\ (type(E1) <: E2) .

  op InvalidSubTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidSubTypeCast)
    if (OP == as) /\ (E2 <: type(E1)) .

  op InvalidReferentialTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidReferentialTypeCast)
    if (OP == as?) /\ (type(E1) :: ReferentialType) /\ (E2 :: ReferentialType) .

  --- op InvalidRequiredTypeCast : -> TypeError .
  --- ceq type(E1 OP E2) = Undefined(InvalidRequiredTypeCast)
  ---   if (OP == as?) /\ not (type(E1) :: RequiredType) /\ (E2 :: RequiredType) .
  ---
  --- op InvalidOptionalTypeCast : -> TypeError .
  --- ceq type(E1 OP E2) = Undefined(InvalidOptionalTypeCast)
  ---   if (OP == as?) /\ not (type(E1) :: OptionalType) /\ (E2 :: OptionalType) .

  ceq type(E1 OP E2) = E2
    if (category(OP) == TypeCasting) /\
       (type(E1) :: ReferentialType) /\
       (E2 :: ReferentialType) .

  op InvalidTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidTypeCast)
    if category(OP) == TypeCasting .
endm

mod UNDEFINED-EXPRESSION is
  pr TYPE .
  pr EXPRESSION .

  op NotInferred : -> TypeError .
  eq type(E:Expression) = Undefined(NotInferred) .
endm

mod EXPRESSIONS is
  pr LITERALS .
  pr LOGICAL-EXPRESSION .
  pr ARITHMETIC-EXPRESSION .
  pr STRING-CONCATENATION .
  pr REFERENTIAL-EXPRESSION .
  pr TYPE-CHECKING .
  pr TYPE-CASTING .
  pr UNDEFINED-EXPRESSION .
endm
