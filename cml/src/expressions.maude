
mod OPERATOR is
  sorts Operator Category .

  op category : Operator -> Category .
endm

mod EXPRESSION is
  pr TYPE .
  pr OPERATOR .

  sorts Expression .
  subsort Type < Expression .

  op type : Expression -> Type .
endm

mod LITERAL-EXPRESSION is
  pr EXPRESSION .

  sort Literal .
  subsort Literal < Expression .
endm

mod LITERAL-BOOLEAN is
  pr LITERAL-EXPRESSION .
  pr BOOLEAN-TYPE .

  op True : -> Literal .
  op False : -> Literal .

  eq type(True) = Boolean .
  eq type(False) = Boolean .
endm

mod LITERAL-INTEGER is
  pr LITERAL-EXPRESSION .
  pr NUMERIC-TYPES .
  pr QID .

  sort IntegerLiteral .
  subsort IntegerLiteral < Literal .

  op _i : Qid -> IntegerLiteral .
  var I : IntegerLiteral .
  eq type(I) = Integer .
endm

mod LITERAL-LONG is
  pr LITERAL-EXPRESSION .
  pr NUMERIC-TYPES .
  pr QID .

  sort LongLiteral .
  subsort LongLiteral < Literal .

  op _l : Qid -> LongLiteral .
  var L : LongLiteral .
  eq type(L) = Long .
endm

mod LITERAL-FLOAT is
  pr LITERAL-EXPRESSION .
  pr FLOATING-POINT-TYPES .
  pr QID .

  sort FloatLiteral .
  subsort FloatLiteral < Literal .

  op _f : Qid -> FloatLiteral .
  var F : FloatLiteral .
  eq type(F) = Float .
endm

mod LITERAL-STRING is
  pr LITERAL-EXPRESSION .
  pr STRING-TYPE .
  pr QID .

  sort StringLiteral .
  subsort StringLiteral < Literal .

  op _s : Qid -> StringLiteral .
  var S : StringLiteral .
  eq type(S) = String .
endm

mod LITERALS is
  pr LITERAL-BOOLEAN .
  pr LITERAL-INTEGER .
  pr LITERAL-LONG .
  pr LITERAL-FLOAT .
  pr LITERAL-STRING .
endm

mod INFIX-EXPRESSION is
  pr EXPRESSION .

  sort Infix .
  subsort Infix < Expression .

  op (_ _ _) : Expression Operator Expression -> Infix .
endm

mod LOGICAL-EXPRESSION is
  pr INFIX-EXPRESSION .

  op Logical : -> Category .
  ops and or : -> Operator .

  eq category(and) = Logical .
  eq category(or) = Logical .

  vars E1 E2 : Expression .
  var OP : Operator .

  pr BOOLEAN-TYPE .

  op logical : Expression Operator Expression -> Bool .
  eq logical(E1, OP, E2) = type(E1) == Boolean and type(E2) == Boolean and
                           category(OP) == Logical .

  ceq type(E1 OP E2) = Boolean if logical(E1, OP, E2) .

  op InvalidLogicalExpression : -> TypeError .

  ceq type(E1 OP E2) = Undefined(InvalidLogicalExpression)
    if not logical(E1, OP, E2) and category(OP) == Logical .
endm

mod ARITHMETIC-EXPRESSION is
  pr INFIX-EXPRESSION .

  op Arithmetic : -> Category .
  ops + - / * : -> Operator .

  eq category(+) = Arithmetic .
  eq category(-) = Arithmetic .
  eq category(/) = Arithmetic .
  eq category(*) = Arithmetic .

  vars E1 E2 : Expression .
  var OP : Operator .

  op arithmetic : Expression Operator Expression -> Bool .

  pr NUMERIC-TYPES .
  ceq arithmetic(E1, OP, E2) = category(OP) == Arithmetic
    if numeric(type(E1)) and numeric(type(E2)) .

  pr FLOATING-POINT-TYPES .
  ceq arithmetic(E1, OP, E2) = category(OP) == Arithmetic
    if floating_point(type(E1)) and floating_point(type(E2)) .

  ceq type(E1 OP E2) = type(E2)
    if arithmetic(E1, OP, E2) and type(E1) <: type(E2) .
  ceq type(E1 OP E2) = type(E1)
     if arithmetic(E1, OP, E2) and type(E2) <: type(E1) .

  op InvalidArithmeticExpression : -> TypeError .

  ceq type(E1 OP E2) = Undefined(InvalidArithmeticExpression)
    if category(OP) == Arithmetic .
endm

mod RELATIONAL-EXPRESSION is
  pr INFIX-EXPRESSION .
  pr BOOLEAN-TYPE .
  pr NUMERIC-TYPES .
  pr FLOATING-POINT-TYPES .
  pr STRING-TYPE .

  op Relational : -> Category .
  ops v=, !=, <, <=, >, >= : -> Operator .

  eq category(v=) = Relational .
  eq category(!=) = Relational .
  eq category(<) = Relational .
  eq category(<=) = Relational .
  eq category(>) = Relational .
  eq category(>=) = Relational .

  vars E1 E2 : Expression .
  var OP : Operator .

  op relational : Expression Operator Expression -> Bool .
  ceq relational(E1, OP, E2) = category(OP) == Relational
    if numeric(type(E1)) and numeric(type(E2)) .
  ceq relational(E1, OP, E2) = category(OP) == Relational
    if floating_point(type(E1)) and floating_point(type(E2)) .
  ceq relational(E1, OP, E2) = category(OP) == Relational
    if type(E1) == String and type(E2) == String .

  ceq type(E1 OP E2) = Boolean
    if relational(E1, OP, E2) .

  op IncompatibleRelationalOperands : -> TypeError .
  ceq type(E1 OP E2) = Undefined(IncompatibleRelationalOperands)
    if category(OP) == Relational /\
       (type(E1) :: ValueType) /\
       (type(E2) :: ValueType) /\
       not (type(E1) == Boolean) /\
       not (type(E2) == Boolean) /\
       type(E1) =/= type(E2) .

  op ExpectedRelationalOperands : -> TypeError .
  ceq type(E1 OP E2) = Undefined(ExpectedRelationalOperands)
    if category(OP) == Relational .
endm

mod STRING-CONCATENATION is
  pr INFIX-EXPRESSION .
  pr STRING-TYPE .
  pr VALUE-TYPE .
  pr REFERENCE-TYPE .

  op Concatenation : -> Category .
  op & : -> Operator .

  eq category(&) = Concatenation .

  vars E1 E2 : Expression .
  var OP : Operator .

  op concat : Expression Operator Expression -> Bool .
  ceq type(E1 OP E2) = String
    if category(OP) == Concatenation /\
       ((type(E1) :: ValueType) or (type(E1) :: ReferenceType)) /\
       ((type(E2) :: ValueType) or (type(E2) :: ReferenceType)) /\
       not (E1 :: Type) /\
       not (E2 :: Type) .

  op InvalidConcatentionOperand : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidConcatentionOperand)
    if (category(OP) == Concatenation) .
endm

mod REFERENTIAL-EXPRESSION is
  pr INFIX-EXPRESSION .
  pr REFERENCE-TYPE .
  pr BOOLEAN-TYPE .

  op ReferentialExpression : -> Category .
  ops === !== : -> Operator .
  eq category(===) = ReferentialExpression .
  eq category(!==) = ReferentialExpression .

  vars E1 E2 : Expression .
  var OP : Operator .

  op UnrelatedTypeComparison : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnrelatedTypeComparison)
    if (category(OP) == ReferentialExpression) /\
       (type(E1) :: ReferenceType) /\
       (type(E2) :: ReferenceType) /\
       not (type(E1) <:> type(E2)) .

  ceq type(E1 OP E2) = Boolean
    if (category(OP) == ReferentialExpression) /\
       (type(E1) :: ReferenceType) /\
       (type(E2) :: ReferenceType) .

  op ExpectedReferenceTypeOperands : -> TypeError .
  ceq type(E1 OP E2) = Undefined(ExpectedReferenceTypeOperands)
    if category(OP) == ReferentialExpression .
endm

mod TYPE-CHECKING is
  pr INFIX-EXPRESSION .
  pr REFERENCE-TYPE .
  pr BOOLEAN-TYPE .

  op TypeChecking : -> Category .
  ops is isnt : -> Operator .
  eq category(is) = TypeChecking .
  eq category(isnt) = TypeChecking .

  vars E1 E2 : Expression .
  var OP : Operator .

  op ExpectedTypeCheckOperand : -> TypeError .
  ceq type(E1 OP E2) = Undefined(ExpectedTypeCheckOperand)
    if (category(OP) == TypeChecking) /\
       not (E2 :: Type) .

  op OutOfBoundsTypeCheck : -> TypeError .
  ceq type(E1 OP E2) = Undefined(OutOfBoundsTypeCheck)
    if (category(OP) == TypeChecking) /\
       not (type(E1) <* E2) .

  op UnrelatedTypeCheck : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnrelatedTypeCheck)
    if (category(OP) == TypeChecking) /\
       (type(E1) <* E2) /\
       not (elementaryTypeOf(type(E1)) <:> elementaryTypeOf(E2)) .

  ceq type(E1 OP E2) = Boolean
    if (category(OP) == TypeChecking) /\
       (type(E1) <* E2) /\
       (elementaryTypeOf(type(E1)) <:> elementaryTypeOf(E2)) .

  op InvalidTypeCheck : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidTypeCheck)
    if category(OP) == TypeChecking .
endm

mod TYPE-CASTING is
  pr INFIX-EXPRESSION .
  pr REFERENCE-TYPE .
  pr BOOLEAN-TYPE .

  op TypeCasting : -> Category .
  ops as as? as! : -> Operator .
  eq category(as) = TypeCasting .
  eq category(as?) = TypeCasting .
  eq category(as!) = TypeCasting .

  vars E1 E2 : Expression .
  var OP : Operator .

  op ExpectedTypeCastOperand : -> TypeError .
  ceq type(E1 OP E2) = Undefined(ExpectedTypeCastOperand)
    if (category(OP) == TypeCasting) /\
       not (E2 :: Type) .

  op OutOfBoundsTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(OutOfBoundsTypeCast)
    if (category(OP) == TypeCasting) /\
       (OP =/= as!) /\
       (OP == as? implies minCardinality(E2) == one) /\
       not (type(E1) <* E2) .

  op UnnecessarilyForcedTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnnecessarilyForcedTypeCast)
    if (OP == as!) /\
       (type(E1) <* E2) /\
       (elementaryTypeOf(type(E1)) <: elementaryTypeOf(E2)) .

  op UnnecessarilyConditionalTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(UnnecessarilyConditionalTypeCast)
    if (OP == as?) /\
       (type(E1) <* E2) /\
       (elementaryTypeOf(type(E1)) <: elementaryTypeOf(E2)) .

  op InvalidSubTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidSubTypeCast)
    if (OP == as) /\
       (type(E1) <* E2) /\
       (elementaryTypeOf(E2) <: elementaryTypeOf(type(E1))) .

  op InvalidReferenceTypeOperand : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidReferenceTypeOperand)
    if (OP == as?) /\
       (type(E1) :: ReferenceType) /\
       (E2 :: ReferenceType) .

  ceq type(E1 OP E2) = E2
    if (category(OP) == TypeCasting) /\
       (OP == as implies type(E1) <* E2) /\
       (OP == as? implies minCardinality(E2) == zero) /\
       (elementaryTypeOf(type(E1)) <:> elementaryTypeOf(E2)) .

  op InvalidTypeCast : -> TypeError .
  ceq type(E1 OP E2) = Undefined(InvalidTypeCast)
    if category(OP) == TypeCasting .
endm

mod CONDITIONAL-EXPRESSION is
  pr EXPRESSION .

  op _if_ : Expression Expression -> Expression .

  var Cond Expr Expr2 : Expression .

  eq type(Expr if Cond) = type(
    elementaryTypeOf(type(Expr)),
    zero,
    maxCardinality(elementaryTypeOf(type(Expr)) ?, type(Expr))
  ) .

  op _or?_ : Expression Expression -> Expression .

  op UnnecessaryConditionalExpression : -> TypeError .
  ceq type(Expr or? Expr2) = Undefined(UnnecessaryConditionalExpression)
    if minCardinality(type(Expr)) =/= zero .

  ceq type(Expr or? Expr2) = type(
    elementaryTypeOf(type(Expr)),
    minCardinality(type(Expr2)),
    maxCardinality(type(Expr), type(Expr2))
  ) if elementaryTypeOf(type(Expr2)) <: elementaryTypeOf(type(Expr)) .
  ceq type(Expr or? Expr2) = type(
    elementaryTypeOf(type(Expr2)),
    minCardinality(type(Expr2)),
    maxCardinality(type(Expr), type(Expr2))
  ) if elementaryTypeOf(type(Expr)) <: elementaryTypeOf(type(Expr2)) .

  op if_then_else_ : Expression Expression Expression -> Expression .
  ceq type(if Cond then Expr else Expr2) = type(
    elementaryTypeOf(type(Expr2)),
    minCardinality(type(Expr), type(Expr2)),
    maxCardinality(type(Expr), type(Expr2))
  ) if elementaryTypeOf(type(Expr)) <: elementaryTypeOf(type(Expr2)) .
  ceq type(if Cond then Expr else Expr2) = type(
    elementaryTypeOf(type(Expr)),
    minCardinality(type(Expr), type(Expr2)),
    maxCardinality(type(Expr), type(Expr2))
  ) if elementaryTypeOf(type(Expr2)) <: elementaryTypeOf(type(Expr)) .

  op IncompatibleConditionalOperands : -> TypeError .
  ceq type(if Cond then Expr else Expr2) = Undefined(IncompatibleConditionalOperands)
    if not (elementaryTypeOf(type(Expr)) <:> elementaryTypeOf(type(Expr2))) .
  ceq type(Expr or? Expr2) = Undefined(IncompatibleConditionalOperands)
    if not (elementaryTypeOf(type(Expr)) <:> elementaryTypeOf(type(Expr2))) .
endm

mod UNDEFINED-EXPRESSION is
  pr TYPE .
  pr EXPRESSION .

  op NotInferred : -> TypeError .
  eq type(E:Expression) = Undefined(NotInferred) .
endm

mod EXPRESSIONS is
  pr LITERALS .
  pr LOGICAL-EXPRESSION .
  pr ARITHMETIC-EXPRESSION .
  pr RELATIONAL-EXPRESSION .
  pr STRING-CONCATENATION .
  pr REFERENTIAL-EXPRESSION .
  pr TYPE-CHECKING .
  pr TYPE-CASTING .
  pr CONDITIONAL-EXPRESSION .
  pr UNDEFINED-EXPRESSION .
endm
